OnMainMenu : all
{
	"\0\*\b2\![set,autoscroll,disable]%(s)"
	
	if meltphase == 1 || meltphase == 2
	{
		"Please put me in the freezer.\_q"
		"\n\n\![*]\q[Ok,OnFreezeCube]  \![*]\q[No,cancel]\n\n[half]"
	}
	else; "\_q"
	"\![*]\__q[OnNewCube]Get a new ice cube\__q\n\n"
	
	"\![*]\__q[OnStats]Stats\__q"
	"  "
	if meltphase == 3 && CurrentItem != ""; "\![*]\_a[OnCollectItem]Collect item\_a"
	else; "\![*]\__q[OnCollectionView]Collection\__q"
	"\n\n[half]"
	
	"\![*]\__q[OnSettings]Settings\__q\n"
	"\![*]\q[Cancel,cancel]"
}

OnFreezeCube
{
	phasecountdown = GETSECCOUNT
	meltphase = 0
	
	s
}

OnNewCube
{
	SaveCubeTime
	meltphase = 0
	cubelifestart = GETSECCOUNT
	phasecountdown = GETSECCOUNT
	
	s("new")
}

OnStats : all
{
	"\0\b2\*\_q\![set,autoscroll,disable]"
	
	_currentcubelife = 0
	
	if meltphase == 3
	{
		_currentcubelife = CubeLives[ARRAYSIZE(CubeLives) - 1]
	}
	else
	{
		_sec = GETSECCOUNT
		_currentcubelife = _sec - cubelifestart
	}
	
	_longestcube = MAX(CubeLives)
	_avgcube = AVERAGE(CubeLives)
	_totalcube = SumArray(CubeLives)
	_cubenum = ARRAYSIZE(CubeLives)
	
	_itemnum = 0
	foreach ItemsCollected; _item
	{
		_itemnum += TOINT(_item[1])
	}
	
	_h = "\f[color,default.anchor]"
	_d = "\f[color,default]"
	
	"\f[align,center]Statistics\n\n\f[align,left]"
	"Current lifespan: %(_h)%(FormatTime(_currentcubelife))%(_d)\n"
	"Longest lifespan: %(_h)%(FormatTime(_longestcube))%(_d)\n"
	"Average lifespan: %(_h)%(FormatTime(_avgcube))%(_d)\n"
	"Total lifespan: %(_h)%(FormatTime(_totalcube))%(_d)\n"
	"Total cubes: %(_h)%(_cubenum)%(_d)\n"
	"Items collected: %(_h)%(_itemnum)%(_d)\n\n"
	
	"\![*]\q[Back,OnMainMenu]  \![*]\q[Close,cancel]"
}

OnCollectItem
{
	"\![bind,Object,,0]"
}

OnCollectionView : all
{
	if reference0 == ""; reference0 = "Fruit"
	"\C\![lock,balloonrepaint]\c"
	"\0\b[2]\_q\![set,autoscroll,disable]\*\f[align,center]Collection\n\n\f[align,left]"
	
	_categories = IARRAY
	_items = ""
	_itemarray = ASORT("string,ascending",ItemDisplayInfo)
	foreach _itemarray; _item
	{
		if _item == ""; continue
		
		if ASEARCH(_item[0],_categories) == -1; _categories ,= _item[0]
		
		if _item[0] == reference0
		{
			_name = _item[1]
			
			_index = -1
			for _i = 0; _i < ARRAYSIZE(ItemsCollected); _i++
			{
				_item = ItemsCollected[_i]
				if _item[0] == _name
				{
					_index = _i
					break
				}
			}
			_num = TOINT(ItemsCollected[_index][1])
			
			if _num > 0
			{
				if ASEARCH(_name,DisplayedItems) != -1; _items += "\_a[OnDisplayItem,%(_name),remove,%(reference0)]%(_name)\_a"
				else; _items += "\__q[OnDisplayItem,%(_name),add,%(reference0)]%(_name)\__q"
			}
			else; _items += "%(_name)"
			
			_items += ": \f[color,default.anchor]%(_num)\f[color,default]"
			
			_items += "\n"
		}
	}
	
	_categories = ASORT("string,ascending",_categories)
	foreach _categories; _category
	{
		if _category == reference0; "\_a[OnCollectionView,%(_category)]%(_category)\_a"
		else; "\__q[OnCollectionView,%(_category)]%(_category)\__q"
		"  "
	}
	"\n\n"
	_items
	
	"\n"
	"\![*]\q[Back,OnMainMenu]  \![*]\q[Close,cancel]"
	"\![unlock,balloonrepaint]"
}

OnDisplayItem
{
	
	
	if reference1 == "remove"
	{
		_index = ASEARCH(reference0,DisplayedItems)
		if _index != -1
		{
			DisplayedItems[_index] = IARRAY
		}
	}
	else
	{
		DisplayedItems ,= reference0
	}
	"\C"
	--
	s
	--
	"\![raise,OnCollectionView,%(reference2)]"
}

OnSettings : all
{
	_talkrate = "Off"
	if aitalkinterval == 30; _talkrate = "30 seconds"
	elseif aitalkinterval == 60; _talkrate = "1 minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	
	"\0\*\b2\![set,autoscroll,disable]\_q\f[anchorvisitedfontcolor,default.anchor]"
	"\f[align,center]Settings\n\n\f[align,left]"
	
	"Talkrate: \_a[OnTalkRateMenu]%(_talkrate)\_a\n\n"
	"\![*]\f[bold,1]\q[Reset,OnResetCheck]\f[bold,default]\n\n"
	"\![*]\q[Back,OnMainMenu]  \![*]\q[Close,cancel]"
}

OnTalkRateMenu : all
{	
	//This is just setting up a label, you may need to add to it or tweak it if you change what rates are available
	_talkrate = "Off"
	if aitalkinterval == 30; _talkrate = "30 seconds"
	elseif aitalkinterval == 60; _talkrate = "1 minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	
	"\0\b2\![set,autoscroll,disable]\_q"
	"Current interval: %(_talkrate)\n\n"
	
	"\![*]\q[Off,OnTalkRateChange,0]\n"
	"\![*]\q[30 seconds,OnTalkRateChange,30]\n"
	"\![*]\q[1 minute,OnTalkRateChange,60]\n"
	"\![*]\q[3 minutes,OnTalkRateChange,180]\n"
	"\![*]\q[5 minutes,OnTalkRateChange,300]\n"
	"\![*]\q[10 minutes,OnTalkRateChange,600]\n"
	"\n"
	"\![*]\q[Back,OnSettings]  \![*]\q[Close,cancel]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
	OnSettings
}

OnResetCheck : all
{
	"\0\b2\![set,autoscroll,disable]\_q"
	"Warning: this will reset all of your stats AND your item collection. This cannot be undone. Do you want to reset?"
	"\n\n\![*]\__q[OnFirstBoot]Yes, reset everything\__q    \![*]\__q[OnSettings]No, don't reset\__q"
}