//———————————————————— Boot ————————————————————
OnFirstBoot
{
	CubeLives = IARRAY //Time counts of all cubes over time
	ItemsCollected = IARRAY
	foreach ItemMasterList; _item
	{
		ItemsCollected ,= "%(_item),0"
	}
	
	"\0\b2KEEP FROZEN\w8\n\nSTORE AT 0°F/-18°C\w8\n\nEXCURSIONS PERMITTED TO 37°F/2.7°C\w8\n\nPRODUCT MAY LEAK OR HAVE CHANGES IN TEXTURE IF ALLOWED TO THAW"
}

OnInitialize
{
	if reference0 != "reload"
	{
		meltphase = 0
		cubelifestart = GETSECCOUNT
		phasecountdown = GETSECCOUNT
		AvailableItems = ItemMasterList
		LastTalk = ""
	}
}

OnDestroy
{
	if reference0 != "reload"
	{
		SaveCubeTime
	}
}

OnBoot
{
	s("new")
	--
	"boot (to the head)"
}

OnClose
{
	
}


//———————————————————— Surface related ————————————————————
OnSurfaceRestore
{
	s
}

OnWindowStateRestore
{
	s
}

//Surface refresh
s : all
{
	"\0\s[%(meltphase)]"
	
	for _i = 0; _i < ARRAYSIZE(SHIORI3FW.IsVisible); _i++
	{
		_entry = ItemDisplayInfo[_i]
		
		if _i == 0; continue
		
		if ASEARCH(_entry[1],DisplayedItems) != -1
		{
			"\p[%(_i)]\s[%(_entry[2])]"
			
		}
		elseif SHIORI3FW.IsVisible[_i] == 1
		{
			"\p[%(_i)]\s[-1]"
		}
	}
	
	"\0"
	if _argv[0] == "new"
	{
		_item = ANY(AvailableItems)
		AvailableItems[LSO] = IARRAY
		if ARRAYSIZE(AvailableItems) == 0; AvailableItems = ItemMasterList
		"\![bind,Object,,0]\![bind,Object,%(_item),1]"
	}
}

//[character ID, category name, part name, option, on-1/off-0, thumbnail path]
OnNotifyDressupInfo
{
	ItemMasterList = IARRAY
	CurrentItem = ""
	foreach reference; _dressup
	{
		_dressup = SPLIT(_dressup,C_BYTE1)
		if _dressup[1] != "Object"; continue
		
		ItemMasterList ,= _dressup[2]
		
		if _dressup[4] == "1"; CurrentItem = _dressup[2]
	}
}


//———————————————————— Misc ————————————————————
OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	TOSTR(_talk)
}

OnAiTalk
{
	if CHAIN.IDName == ""
	{
		LastTalk = RandomTalk
	}
	else
	{
		LastTalk = ChainTalk
	}
	LastTalk
}

OnKeyPress
{
	if reference0 == "t"; "\![raise,OnAiTalk]"
	elseif reference0 == "r"; LastTalk
}

OnMouseDoubleClick
{
	if reference5 == 0 && reference3 == 0; OnMainMenu
}

OnSecondChange
{
	_sec = GETSECCOUNT
	_time = _sec - phasecountdown
	
	_newphase = 0
	case _time
	{
		when 0-1200 //Full
		{
			_newphase = 0
		}
		when 1201-2100 //Mid
		{
			_newphase = 1
		}
		when 2101-2700 //Tiny
		{
			_newphase = 2
		}
		others //Puddle
		{
			_newphase = 3
		}
	}
	
	//1200 - 20 mins
	//2100 - 20 mins + 15 mins
	//2700 - 20 mins + 15 mins + 10 mins
	if _newphase != meltphase
	{
		meltphase = _newphase
		
		//Add item to collection
		if meltphase == 3
		{
			_found = 0
			for _i = 0; _i < ARRAYSIZE(ItemsCollected); _i++
			{
				_item = ItemsCollected[_i]
				if _item[0] == CurrentItem
				{
					AddToCollection(_i)
					_found = 1
					break
				}
			}
			
			//Initialize if not found
			if _found == 0
			{
				ItemsCollected ,= "%(CurrentItem),1"
			}
			SaveCubeTime
		}
		
		if OpenBalloon; "\C"
		--
		s
	}
}
