//This file handles a couple little QOL functions you may want, as well as some SNTP events since they don't have default text. But these are not necessary! If you don't care for them just erase them. If you don't want to set up network updates, you can comment or remove the update functions too. If you're not interested in any of these, just remove the whole file!

//—————————————————————————————— Installing something ——————————————————————————————
OnInstallComplete
{
	"\0%(s)%(ToMeltCase('Installed')) %(ToMeltCase(reference1)) (%(ToMeltCase(reference0)))."
	--
	if "ghost" _in_ reference0 //A little menu to open or change to the ghost that was installed immediately
	{
		"\n\n\_q"
		--
		"\![*]\__q[OnInstallComplete.Action,call]Call %(reference0)\__q  \![*]\__q[OnInstallComplete.Action,change]Change to %(reference0)\__q\n\n"
		--
		"\![*]\q[Thanks,blank]"
	}
}

OnInstallComplete.Action
{
	if reference0 == "change"
	{
		"\![change,ghost,lastinstalled]"
	}
	else //call
	{
		"\![call,ghost,lastinstalled]"
	}
}


//—————————————————————————————— Creating a .nar file ——————————————————————————————
OnNarCreated
{
	_nar = "\_a[OnNarLocation,""%(reference1)""]%(reference0)\_a" //Makes a clickable link that'll open the location of the file they just made
	
	"\0%(s)" + ToMeltCase("Successfully Created ") + "%(ToMeltCase(_nar))."
}

OnNarLocation
{	
	_path = SPLITPATH(reference0)[0] + SPLITPATH(reference0)[1]
	
	"\![open,file,""%(_path)""]"
}


//—————————————————————————————— Network Update ——————————————————————————————
//Starting an update
OnUpdateBegin
{
	"\0%(s)" + ToMeltCase("Checking For Updates.") + "\w8"
}

//reference0 is the number of new files, starting from 0
OnUpdateReady
{
	reference0++ //Number of files starts from 0, so this displays the correct amount
	
	//plural checks, adds an s and changes is to are if there's more than 1 file
	_s = ""; _are = "Is"
	if reference0 != 1; {_s = "s"; _are = "Are"}
	
	"\0%(s)" + ToMeltCase("There %(_are) %(reference0) New File%(_s).") + "\w8"
}

//When the update finishes. Don't forget to initialize any new variables you've created! I highly highly recommend using OnInitialize for this (it's in boot.dic), in case the user updates via the ghost explorer or some other means.
//reference0 is 'none' if there were no new files to update with, reference1 is a list of new file names, separated by commas
OnUpdateComplete : all
{
	if reference0 == "none"
	{
		"%(s)" + ToMeltCase("There are no new files.")
	}
	else
	{
		"%(s)"
		if CurrentItem == ""; "\![bind,Object,Candy Cane - Red,1]" //Apology for renumbering stuff and making ice cube therefore be empty upon updating lol
		else; "\![bind,Object,%(CurrentItem),1]"
		ToMeltCase("Update complete.")
	}
}

//reference0 is the reason the update failed
OnUpdateFailure //If the update fails
{
	"\0%(s)"
	--
	if reference0 == "timeout" //If it timed out
	{
		ToMeltCase("Connection Timed Out.")
	}
	elseif reference0 == "md5 miss" //If the update files have not been uploaded correctly and don't match the updates2.dau
	{
		_file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] //This gets the name and extension of the offending file
		
		ToMeltCase("The MD5 Values Don't Match.\w8 Contact The Ghost Author And Let Them Know That '%(_File)' Is Giving An MD5 Error.")
	}
	elseif reference0 == "artificial" //If the user cancels the update by double clicking the balloon
	{
		ToMeltCase("Update Cancelled.")
	}
	else //Any other errors
	{
		ToMeltCase("Error while updating: ""%(reference0)"".")
	}
}


//—————————————————————————————— SNTP (clock fixing) ——————————————————————————————
OnSNTPCompare
{
	"\0%(s)"
	--
	if reference3 != 0
	{
		_s = ""; if reference3 != 1; _s = "s"
		
		ToMeltCase("The Clock Is Off By %(reference3) Second%(_s).") + "\n\n"
		--
		"\![*]\q[Fix it,script:\6]  \![*]\q[Leave it,blank]"
	}
	else
	{
		ToMeltCase("The Clock Is Accurate.")
	}
}

OnSNTPCorrect
{
	"\0%(s)"
	--
	ToMeltCase("The Clock Has Been Corrected.")
}

OnSNTPFailure
{
	"\0%(s)"
	--
	ToMeltCase("Could Not Correct The Clock.")
}