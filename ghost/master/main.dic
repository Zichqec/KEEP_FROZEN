//———————————————————— Boot ————————————————————
OnFirstBoot
{
	CubeLives = IARRAY //Time counts of all cubes over time
	ItemsCollected = IARRAY
	refusals = 0
	AvailableItems = ItemMasterList
	AutoAdjust = 1
	DisplayedItems = IARRAY
	ItemsThatHaveBeenDisplayed = IARRAY
	meltphase = 0
	cubelifestart = GETSECCOUNT
	phasecountdown = GETSECCOUNT
	LastTalk = ""
	
	foreach ItemMasterList; _item
	{
		ItemsCollected ,= "%(_item),0"
	}
	
	"\0\s[0]\![bind,Object,,0]\![bind,Object,Sea Purple,1]" + BootText
}

OnInitialize
{
	if reference0 != "reload"
	{
		meltphase = 0
		cubelifestart = GETSECCOUNT
		phasecountdown = GETSECCOUNT
		AvailableItems = ItemMasterList
		LastTalk = ""
		if !ISVAR("AutoAdjust"); AutoAdjust = 1
		if !ISVAR("ItemsThatHaveBeenDisplayed"); ItemsThatHaveBeenDisplayed = IARRAY
	}
}

OnDestroy
{
	if reference0 != "reload"
	{
		if meltphase != 3; SaveCubeTime
	}
}

OnBoot
{
	s("new")
	--
	BootText
}

BootText
{
	"\0%(s)\b2KEEP FROZEN\w8\n\nSTORE AT 0°F/-18°C\w8\n\nEXCURSIONS PERMITTED TO 37°F/2.7°C\w8\n\nPRODUCT MAY LEAK OR HAVE CHANGES IN TEXTURE IF ALLOWED TO THAW"
}

OnClose : all
{
	"\0\b2"
	switch meltphase
	{
		"FREEZE FOR 2-4 HOURS AT 0°F/-18°C\w8\n\nFROZEN CUBES SHOULD BE REMOVED FROM ICE TRAY AND STORED IN A SEALED CONTAINER UNTIL USE"
		"Freeze For 4-6 Hours At%(br)0°F/-18°C\w8\n\nFrozen Cubes Should Be Removed From Ice Tray And Stored In A Sealed Container Until Use"
		"freeze for 8-10 hours at%(br)0°f/-18°c\w8\n\nfrozen cubes should be removed from ice tray and stored in a sealed container until use"
	}
	"\w8\w8\w8\-"
}


//———————————————————— Surface related ————————————————————
OnSurfaceRestore
{
	s
}

OnWindowStateRestore
{
	s
}

//Surface refresh
s : all
{
	"\0\s[%(meltphase)]"
	
	for _i = 0; _i < ARRAYSIZE(SHIORI3FW.IsVisible) || _i < ARRAYSIZE(ItemDisplayInfo); _i++ //TODO double check this all works as intended
	{
		_entry = ItemDisplayInfo[_i]
		
		if _i == 0; continue // ??? is there a reason i didn't just start this loop at 1
		
		if ASEARCH(_entry[1],DisplayedItems) != -1
		{
			"\p[%(_i)]\s[%(_entry[2])]"
			
		}
		elseif SHIORI3FW.IsVisible[_i] == 1
		{
			"\p[%(_i)]\s[-1]"
		}
	}
	
	"\0"
	if ARRAYSIZE(AvailableItems) == 0; AvailableItems = ItemMasterList
	if _argv[0] == "new"
	{
		_item = ANY(AvailableItems)
		AvailableItems[LSO] = IARRAY
	
		"\![bind,Object,,0]\![bind,Object,%(_item),1]"
	}
}

//[character ID, category name, part name, option, on-1/off-0, thumbnail path]
OnNotifyDressupInfo
{
	ItemMasterList = IARRAY
	CurrentItem = ""
	foreach reference; _dressup
	{
		_dressup = SPLIT(_dressup,C_BYTE1)
		if _dressup[1] != "Object"; continue
		
		ItemMasterList ,= _dressup[2]
		
		if _dressup[4] == "1"; CurrentItem = _dressup[2]
	}
}


//———————————————————— Misc ————————————————————
OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	if "\![is-aitalk]" _in_ _talk && AutoAdjust
	{
		// _length = ARRAYSIZE(SPLIT(_talk," "))
		// LOGGING("====================================================================")
		// LOGGING("_length: %(_length)")
		// if _length >= 30; _talk = "\b[4]" + _talk
		// elseif _length >= 15; _talk = "\b[2]" + _talk
		_len = STRLEN(_talk) - STRLEN("\![is-aitalk]")
		LOGGING("====================================================================")
		LOGGING("_len: %(_len)")
		if _len >= 400; _talk = "\b[4]" + _talk
		elseif _len >= 200; _talk = "\b[2]" + _talk
	}
	
	TOSTR(_talk)
}

OnAiTalk
{
	if CHAIN.IDName == ""
	{
		LastTalk = RandomTalk
	}
	else
	{
		LastTalk = ChainTalk
	}
	LastTalk
}

OnKeyPress
{
	if reference0 == "t"; "\![raise,OnAiTalk]"
	elseif reference0 == "r"; LastTalk
}

OnMouseDoubleClick
{
	if reference5 == 0 && reference3 == 0; OnMainMenu
}

OnSecondChange
{
	LOGGING("====================================================================")
	LOGGING("OnSecondChange")
	_sec = GETSECCOUNT
	_time = _sec - phasecountdown
	
	LOGGING("_sec: %(_sec)")
	LOGGING("_time: %(_time)")
	
	
	_newphase = 0
	case _time
	{
		when 0-1199 //Full
		{
			_newphase = 0
		}
		when 1200-2099 //Mid
		{
			_newphase = 1
		}
		when 2100-2699 //Tiny
		{
			_newphase = 2
		}
		others //Puddle
		{
			_newphase = 3
		}
	}
	LOGGING("_newphase: %(_newphase)")
	LOGGING("meltphase: %(meltphase)")
	LOGGING("cubelifestart: %(cubelifestart)")
	LOGGING("phasecountdown: %(phasecountdown)")
	
	//1200 - 20 mins
	//2100 - 20 mins + 15 mins
	//2700 - 20 mins + 15 mins + 10 mins
	if _newphase != meltphase
	{
		LOGGING("newphase != meltphase")
		meltphase = _newphase
		LOGGING("new meltphase: %(meltphase)")
		//Add item to collection
		if meltphase == 3
		{
			_found = 0
			for _i = 0; _i < ARRAYSIZE(ItemsCollected); _i++
			{
				_item = ItemsCollected[_i]
				if _item[0] == CurrentItem
				{
					AddToCollection(_i)
					_found = 1
					break
				}
			}
			
			//Initialize if not found
			if _found == 0
			{
				ItemsCollected ,= "%(CurrentItem),1"
			}
			SaveCubeTime
		}
		
		if OpenBalloon; "\C"
		--
		s
	}
	LOGGING
	LOGGING
}

On_homeurl
{
	"https://raw.githubusercontent.com/Zichqec/KEEP_FROZEN/main/"
}